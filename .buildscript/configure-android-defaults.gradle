apply plugin: 'cc-register'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file(".buildscript/configure-setting-dagger.gradle")

android {
    compileSdkVersion Build.compileSdk
    buildToolsVersion Build.buildTools
    defaultConfig {
        minSdkVersion Build.minSdk
        targetSdkVersion Build.targetSdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            // 是否进行混淆
            minifyEnabled true
            // 开启zipAlign可以让安装包中的资源按4字节对齐，这样可以减少应用在运行时的内存消耗
            zipAlignEnabled true
            // 移除无用的resource文件
            // 需要注意的是目前资源压缩器目前不会移除values/文件夹中定义的资源（例如字符串、尺寸、样式和颜色）
            shrinkResources true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }
    viewBinding {
        enabled = true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        unitTests {
            // Fix unit test resources not included with AS3
            includeAndroidResources = true
        }
    }
    splits {
        abi {
            enable = true
            reset()
            include("armeabi-v7a", "x86") // "arm64-v8a" "x86_64"
            universalApk = false
        }
    }
    dexOptions {
        jumboMode true
        javaMaxHeapSize "4g"
    }
    aaptOptions {
        // 在Android的构建流程中，AAPT会使用内置的
        // 压缩算法来优化res/drawable/目录下的PNG图片，
        // 但这可能会导致本来已经优化过的图片体积变大，
        // 因此需要禁止AAPT来优化PNG图片
        cruncherEnabled = false
    }
}

dependencies {
    implementation project(":common")

    testImplementation(Deps.jUnit)
    androidTestImplementation(Deps.androidJUnit)
    androidTestImplementation(Deps.espresso)

    // Database Debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // 内存泄漏
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
}