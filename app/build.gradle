ext.mainApp = true  // 设置为true，表示此module为主app module，一直以application方式编译
apply from: rootProject.file(".buildscript/configure-android-defaults.gradle")
apply from: rootProject.file(".buildscript/configure-setting-keystore.gradle")

android {
    defaultConfig {
        applicationId Build.applicationId
        versionCode Build.versionCode
        versionName Build.versionName
    }
    // 自定义输出apk名字
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputAbi = output.getFilter(com.android.build.OutputFile.ABI)
            def abiName = System.getenv('GRADLE_ABI')?.toLowerCase() ?: "all"
            if (outputAbi != null) abiName = outputAbi
            output.versionCodeOverride = 10000000 + defaultConfig.versionCode
            def outputName = "DanDanPlay-TV-"
            outputName += variant.versionName + "-" + abiName + ".apk"
            output.outputFileName = outputName
        }
    }
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/armeabi-v8a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }
}

dependencies {
    addComponent 'component_tv', project(":component:tv")
    addComponent 'component_torrent', project(":component:torrent")
}
