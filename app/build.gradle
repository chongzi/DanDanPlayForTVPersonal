ext.mainApp = true  // 设置为true，表示此module为主app module，一直以application方式编译
apply from: rootProject.file(".buildscript/configure-android-defaults.gradle")
apply from: rootProject.file(".buildscript/configure-setting-keystore.gradle")

android {
    // VLC 需要
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId Build.applicationId
        versionCode Build.versionCode
        versionName Build.versionName
    }
    // 自定义输出apk名字
    def abiCodes = ['x86': 5, 'x86_64': 8, 'armeabi-v7a': 4, 'arm64-v8a': 7]
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputAbi = output.getFilter(com.android.build.OutputFile.ABI)
            def abiName = System.getenv('GRADLE_ABI')?.toLowerCase() ?: "all"
            if (outputAbi != null) abiName = outputAbi
            output.versionCodeOverride = 10000000 + defaultConfig.versionCode + abiCodes.get(abiName, 0)
            def outputName = "DanDanPlay-TV-"
            outputName += variant.versionName + "-" + abiName + ".apk"
            output.outputFileName = outputName
        }
    }
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/armeabi-v8a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }
//    testOptions {
//        unitTests {
//            // Fix unit test resources not included with AS3
//            includeAndroidResources = true
//        }
//    }
    splits {
        abi {
            enable = true
            reset()
            include("armeabi-v7a", "x86") // "arm64-v8a" "x86_64"
            universalApk = false
        }
    }
//    dexOptions {
//        jumboMode true
//        javaMaxHeapSize "4g"
//    }
    aaptOptions {
        // 在Android的构建流程中，AAPT会使用内置的
        // 压缩算法来优化res/drawable/目录下的PNG图片，
        // 但这可能会导致本来已经优化过的图片体积变大，
        // 因此需要禁止AAPT来优化PNG图片
        cruncherEnabled = false
    }
}

dependencies {
    addComponent 'component_tv', project(":component:tv")
    addComponent 'component_torrent', project(":component:torrent")
    addComponent 'component_player', project(":component:player")
}
